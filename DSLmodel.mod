#####################################################################
# Daily Soil Loss model - DSL 
#
# water balance and erosion on daily basis 
# Infiltration and runoff and flow and splash erosion 
# are done in 10 min intervals with a kinematic wave for Q and V
# this model does NOT do river flow and erosion, only slopes
#
# V. Jetten 16 Nov 2024, ITC
# version 1.3 GITHUB
#####################################################################

# arguments to run:
# $1 folder where the model is $2=raindirr $3=mapsdir $4=resultdir $5=somestring $6=infiltration $7=cohesion
# assumed folder structure
# modeldir
# modeldir/rain
# modeldir/maps
# modeldir/res


#! --matrixtable --radians --lddout 

binding

### input maps ###
  # these maps are generated by lisdbasegenerator: 
  # https://github.com/vjetten/openLISEMdata/releases/tag/LISEM 

  ### input meteo and NDVI
  raininput = "$1/$2/CHIR";   # list of maps CHIR0000.001, 002 etc , name is CASE SENSITIVE !
  ETpinput  = "$1/$2/ERA5_";    # list of maps ERA00000.001, 002 etc

  #  homogeneous = boolean(0); 
  # make the rain homogeneous (area avreage of image) for testing! 
  # 1 = homogeneous, 0 is image. So you see the influence of the land use and slope
  # without rainfall variability
  
  ndvi0 = "$1/$2/ndvi_1.map";
  ndvi1 = "$1/$2/ndvi_2.map";
  ndvi2 = "$1/$2/ndvi_3.map";
  ndvi3 = "$1/$2/ndvi_4.map"; 
  ndvi4 = "$1/$2/ndvi_5.map";
  ndvi5 = "$1/$2/ndvi_6.map";
  ndvi6 = "$1/$2/ndvi_7.map";
  ndvi7 = "$1/$2/ndvi_8.map";
  ndvi8 = "$1/$2/ndvi_8.map";

  ### input maps

# landscape and river
  DEM = "$1/$3/dem.map";
  LDD = "$1/$3/ldd.map";
  chanmask = "$1/$3/chanmask.map";
  chanwidth = "$1/$3/chanwidt.map";
# surface
  landuse = "$1/$3/landuse.map";
  Smax = "$1/$3/smax.map";                  # max canopy storage, interception mm
  RR = "$1/$3/rr.map";                      # micro roughness (cm)
# soil
  Ksat1 = "$1/$3/ksat1.map";                # ksat map (mm/h)
  theta_s = "$1/$3/thetas1.map";            # porosity (fraction)
  thetainit = "$1/$3/thetai1.map";          # initial moisture content (fraction)
  theta_fc = "$1/$3/fieldcap1.map";         # field capacity (fraction)
  theta_wp = "$1/$3/wilting1.map";          # wilting point (fraction)
  SD1 = "$1/$3/soildep1.map";               # soildepth root zone (mm)
# erosion
  Kfactor = "$1/$3/aggrstab.map";   	     # splash erosion g/J (EUROSEM)
  Coh = "$1/$3/coh.map";                    # coh soil kPa
  CohVeg = "$1/$3/cohadd.map";              # coh roots kPa
  D50 = "$1/$3/d50.map";                    # Median grainsize distribution (micrometer)
  PH = "$1/$3/ch.map";                      # plant height m 

# not used
#  LDDchan ="$1/$3/lddchan.map";

### calibration factors ###

  infil_cal = $6;     # lower is more runoff, calibration factor Ksat, influences infiltration  
                      # calibrate until the fraction runoff is acceptable
  coh_cal = $7;       # lower is more flow erosion 
 
  Splash_cal = 1.0;   # lower is less splash erosion  
 
  rain_fact = 1.0;    # CHECK if the rainfall images are in mm/day, if so set this factor to 1.0 

### Output maps
 
  SoilMoisture = "RES/moist$5a";              # daily soil moisture maps (mm)
  Pcum = "$1/$4/raintotal$5.map";               # cumulative rainfall map (mm)
  ds_map = "$1/$4/ds$5.map";                    # splash detachment ton/ha
  df_map = "$1/$4/df$5.map";                    # flow detachment ton/ha
  dep_map = "$1/$4/dep$5.map";                  # deposition detachment ton/ha
  Soilloss_map = "$1/$4/Soilloss$5.map";        # soiloss detachment ton/ha
  erosrate_map = "$1/$4/SoilLossClass$5.map";   # classified soil loss (1-6)
  rocum_map = "$1/$4/rocum$5.map";                  # cumulative water available for runff (mm)
  rocumm3_map = "$1/$4/ro_m3$5.map";                # cumulative runoff flow pattern (m3)
  infiltot_map = "$1/$4/infiltot$5.map";
  intctot_map = "$1/$4/intctot$5.map";
  etatot_map = "$1/$4/etatot$5.map";
  ptot_map = "$1/$4/Ptot$5.map";
# output tables

  P_tss = "$1/$4/Pday$5.tss";                # average daily Rain mm
  Pcum_tss = "$1/$4/Pcum$5.tss";		      # average cumulative Rain mm
  ETpcum_tss = "$1/$4/ETpcum$5.tss";         # average cumulative ETp mm
  ETpavg_tss = "$1/$4/ETpday$5.tss";         # average daily ETp (mm)
  eta_tss = "$1/$4/ETaday$5.tss";            # average daily ETa (mm)
  etacum_tss = "$1/$4/ETacum$5.tss";         # average cumulative ETa (mm)
  intccum_tss = "$1/$4/interccum$5.tss";     # average cumulative interception (mm)
  infil_tss = "$1/$4/infcum$5.tss";          # average infiltration (mm)
  rocum_tss = "$1/$4/rocum$5.tss";          # average WH surface (mm)
  rofract_tss = "$1/$4/rofract$5.tss";          # average WH surface (mm)
  perc_tss = "$1/$4/perccum$5.tss";          # average percolation (mm)
  theta_tss = "$1/$4/theta$5.tss";           # average daily theta  (-)
  moisture_tss = "$1/$4/moisture$5.tss";     # average daily soil moisture (mm) 
  LUtheta_tss = "$1/$4/thetaLU$5.tss";       # theta per landuse
  Soillosslu_tss = "$1/$4/SoillossLU$5.tss"; # soil loss per landuse ton/ha
  Soilloss_tss = "$1/$4/Soilloss$5.tss";     # average soil loss ton/ha
  Cover_tss = "$1/$4/cover$5.tss";           # average vegetation cover fraction (-)

  DEMr = "$1/$4/dem.map";                    # copy these maps to output folder for easier checking of results
  landuser = "$1/$4/landuse.map";
  chanmaskr = "$1/$4/chanmask.map";

areamap
  DEM;
   
timer
  1 122 1;  # run days in growing season 122 days

initial

  # report to result directory for reference
  report DEMr = DEM;
  report landuser = landuse;
  report chanmaskr = chanmask;

  ### length rainfall in steps of 10 min
  # 70 min as average for weak and strong tainfall see article of ichoku et al
  # https://journals.ametsoc.org/view/journals/hydr/18/6/jhm-d-17-0068_1.xml
  maxcount = 7; # nr of 10 min timesteps for kin wave
  dt = scalar(600);   # 10 min is 600 sec
  hoursrain = maxcount*dt/3600.0;   # hours it rains, can be fraction

  mask = scalar(DEM gt 0);
  outlet = pit(LDD);
  dx = celllength();
  nrCells = maptotal(if(chanmask eq 1,0,mask)); #maptotal(mask);

  #count = 1*mask; # used to count rainy days
  nLU = ordinal(landuse); # output

#############################################  
### read soil and vegetation related data ###
############################################# 
  
  Kc = 1.0;                      # FAO crop factor Kc
  day = scalar(1);               # day counter, not really used at the moment!

  Ksat = Ksat1 * mask;           # saturated conductivity mm/h
  theta = thetainit;             # initialize soil moisture theta 
  SoilMoisture = theta*SD1;      # initial soil moisture in mm 

  #D50 = D50 + 2; # add 2 mu to adjust clayey soils

  ks = ln(min(1000.0,max(0.5,Ksat)));
  lambda = max(0.1, min(0.7, 0.0849*ks+0.159));	#Brooks Corey lambda needed for percolation
  psi = (exp(-0.3382*ks)+3.3425); #psi suction front infiltration, cm

  # Govers transport capacity factors
  Cg = ((D50+5)/0.32)**-0.6;
  Dg = ((D50+5)/300)**0.25;
  
  Grad = slope(DEM) + 0.005;
  Grad = if(chanmask eq 1,0.01,Grad);

  #https://www.researchgate.net/publication/266227873_A_simple_formula_for_predicting_settling_velocity_of_sediment_particles
  # fall velocity
  GRAV = 9.81;
  Vs = 2*(2650.0-1000.0)*GRAV*((D50/2000000.0)**2)/(9*0.001);  # Stokes settling velocity if D50 < 100

########################################
### Initialize vegetation cover maps ###
########################################  

  daycov0 = 1;
  daycov1 = 16;
  daycov2 = 32;
  daycov3 = 48;
  daycov4 = 54;
  daycov5 = 70;
  daycov6 = 86;
  daycov7 = 102;
  daycov8 = 122;
  #daycov9 = ...   add more intervals if needed

# corrections: if a value in a ndvi map is less than 0.2 (suspiciopus/cloud)
# AND the values of the previous AND next images are higher, so it presents a dip, than take the average 
# of the previous and the next inmage, else take the recorded ndvi value
  thr = 0.2;
 
  ndvia = ndvi0;
  ndvib = ndvi1;
  ndvic = ndvi2;
  ndvi1 = if ((ndvib lt thr) and (ndvia gt thr) and (ndvic gt thr) and (ndvia gt ndvib) and (ndvic gt ndvib),(ndvia+ndvic)/2.0,ndvib);
  
  ndvia = ndvi1;
  ndvib = ndvi2;
  ndvic = ndvi3;
  ndvi2 = if ((ndvib lt thr) and (ndvia gt thr) and (ndvic gt thr) and (ndvia gt ndvib) and (ndvic gt ndvib),(ndvia+ndvic)/2.0,ndvib);
  
  ndvia = ndvi2;
  ndvib = ndvi3;
  ndvic = ndvi4;
  ndvi3 = if ((ndvib lt thr) and (ndvia gt thr) and (ndvic gt thr) and (ndvia gt ndvib) and (ndvic gt ndvib),(ndvia+ndvic)/2.0,ndvib);
  
  ndvia = ndvi3;
  ndvib = ndvi4;
  ndvic = ndvi5;
  ndvi4 = if ((ndvib lt thr) and (ndvia gt thr) and (ndvic gt thr) and (ndvia gt ndvib) and (ndvic gt ndvib),(ndvia+ndvic)/2.0,ndvib);
  
  ndvia = ndvi4;
  ndvib = ndvi5;
  ndvic = ndvi6;
  ndvi5 = if ((ndvib lt thr) and (ndvia gt thr) and (ndvic gt thr) and (ndvia gt ndvib) and (ndvic gt ndvib),(ndvia+ndvic)/2.0,ndvib);
  
  ndvia = ndvi5;
  ndvib = ndvi6;
  ndvic = ndvi7;
  ndvi6 = if ((ndvib lt thr) and (ndvia gt thr) and (ndvic gt thr) and (ndvia gt ndvib) and (ndvic gt ndvib),(ndvia+ndvic)/2.0,ndvib);

  ndvia = ndvi6;
  ndvib = ndvi7;
  ndvic = ndvi8;
  ndvi7 = if ((ndvib lt thr) and (ndvia gt thr) and (ndvic gt thr) and (ndvia gt ndvib) and (ndvic gt ndvib),(ndvia+ndvic)/2.0,ndvib);

#########################
### initialize totals ###
#########################

  interception = 0*mask;
  Pcum = 0*mask;
  ETpcum = 0*mask;
  ETacum = 0*mask;
  intccum = 0*mask; 
  infilcum = 0*mask;
  perccum = 0*mask;
  rocum = 0*mask;
  rocumout = 0*mask;
  Qtot = 0*mask;
  Sed = 0*mask;
  erosion = 0*mask;
  deposition= 0*mask;
  Dscum =0*mask;
  Dfcum = 0*mask;
  Depcum = 0*mask;
  ETfcum = 0*mask;
  Conc = 0*mask;
  Volcum = 0*mask;
  rocumm3 = 0*mask;
 
dynamic

########################
### meteo data input ###
########################

  rainin = timeinput(raininput)*mask/rain_fact;
  
  rain = rainin; 
  #rain = if(homogeneous, areaaverage(rainin,nominal(mask)), rainin);
  
  report P_tss = maptotal(rain)/nrCells;
 
  Pcum = Pcum + rain;
  #calculate cumulative P for outut

  report Pcum_tss = maptotal(Pcum)/nrCells;   
  # write a graph of the average cumulative rainfall

  # nafi & Mersso, Berhan. (2018). 
##  ETo = 4.2*mask;
##  ETo = if(day gt 30, 3.0, ETo);
##  ETo = if(day gt 61, 3.1, ETo);
##  ETo = if(day gt 92, 3.5, ETo);
##  ETo = if(day gt 122, 3.9, ETo);
  # ETo = timeinputscalar(ETo_tss, nominal(mask));    # read potential evapotranspiration from a file and give the whole area that value
 
  ETo = timeinput(ETpinput)*mask;
 
  Kc = 1.0; # FAO crop factor, depends onj growing stage
  ETp = Kc * ETo;
  ETpcum = ETpcum + ETp;

######################################
### vegetation ndvi interpolation ###
######################################     
    
  #linear interpolation between two images
  Ndvi = ndvi0*mask;
  Ndvi = if (day ge daycov0, ndvi0 + (day - daycov0)/(daycov1-daycov0 +0.01)*(ndvi1-ndvi0), Ndvi); 
  Ndvi = if (day ge daycov1, ndvi1 + (day - daycov1)/(daycov2-daycov1 +0.01)*(ndvi2-ndvi1), Ndvi); 
  Ndvi = if (day ge daycov2, ndvi2 + (day - daycov2)/(daycov3-daycov2 +0.01)*(ndvi3-ndvi2), Ndvi); 
  Ndvi = if (day ge daycov3, ndvi3 + (day - daycov3)/(daycov4-daycov3 +0.01)*(ndvi4-ndvi3), Ndvi); 
  Ndvi = if (day ge daycov4, ndvi4 + (day - daycov4)/(daycov5-daycov4 +0.01)*(ndvi5-ndvi4), Ndvi); 
  Ndvi = if (day ge daycov5, ndvi5 + (day - daycov5)/(daycov6-daycov5 +0.01)*(ndvi6-ndvi5), Ndvi); 
  Ndvi = if (day ge daycov6, ndvi6 + (day - daycov6)/(daycov7-daycov6 +0.01)*(ndvi7-ndvi6), Ndvi); 
  Ndvi = if (day ge daycov7, ndvi7 + (day - daycov7)/(daycov8-daycov7+0.01)*(ndvi8-ndvi7), Ndvi); 
  Ndvi = if (day ge daycov8, ndvi8, Ndvi); 
 # Ndvi = if (day ge daycov8, ndvi8 + (day - daycov8)/(daycov9-daycov8+0.01)*(ndvi9-ndvi8), Ndvi); 
 # Ndvi = if (day ge daycov9, ndvi9 + (day - daycov9)/(daycov10-daycov9+0.01)*(ndvi10-ndvi9), Ndvi); 
 # Ndvi = if (day ge daycov10, ndvi10 + (day - daycov10)/(daycov11-daycov10+0.01)*(ndvi11-ndvi10), Ndvi); 
 # Ndvi = if (day ge daycov11, ndvi11 + (day - daycov11)/(daycov12-daycov11+0.01)*(ndvi12-ndvi11), Ndvi); 
 # Ndvi = if (day ge daycov11, ndvi12, Ndvi); 

  Ndvi = cover(Ndvi,0)*mask;
  NDVI = min(1.0,max(0,Ndvi));                         
  #make sure NDVI is between 0 and 1!
  
  #Cover = 1 -exp(-2*NDVI/(1.5 - NDVI));              # Converting NDVI values to Cover factor according to Van der Knijff et al., 1999
  a_ = 4.257;
  b_ = 100.719;
  c_ = -5.439;
  Cover = min(0.99,max(0.0,a_*NDVI*NDVI + b_*NDVI + c_)/100.0)*mask;
  Cover = if(chanmask eq 1, 0, Cover); # no cover in channels

  N = 0.01*RR + 0.1*Cover;   # mannings n depends on cover, undocumented LISEM equation
  N = if(chanmask eq 1,0.05,N);

  Cohesion = coh_cal*max(-1,Coh + Cover*CohVeg);
  Cohesion = if (chanmask eq 1,-1,Cohesion);

  chanwidth = cover(chanwidth,0);

#################################
######### Interception ##########
#################################

  interception = interception + Cover*(rain - ETp);         # add rainfall and subtract evaporation from interception
  interception = min(Smax*Cover, interception);             # fill up the interception with rain, make sure water has Smax = 0   
  interception = max(0, interception);                      # interception cannot be less than 0
  Pe = max(rain - interception, 0);                         # Estimating effective rainfall (mm)
  intccum = intccum + interception;                         # cumulating interception
 
###############################
### Infiltration and Runoff ###
###############################

  count = 0;

  W = dx/3; # flow width m, water is concentrated in 1/3 of the gridcell, rill density
  Infilday = 0;  # to calc daily infiltration
  WHavg = 0*mask;
  Volavg = 0*mask;
  Peday = 0; 
  division = 2.0;
  ###loop that does the fast processes per 10 min and uses a kin wave for routing
  repeat {
     
   #################################
   ######### Surface water #########
   #################################

    Pinterval = Pe/division;
    division *= 2.0;
    # this gives a rainfall for each step that is Pe/2, Pe/4, Pe/8 etc. an exponential decline
    # or some formula
    #Pinterval = Pe*exp(-0.693*(count+1)); #mm/dt

    Peday = Peday + Pinterval; # add rainfall of this day, must become Pe
 
    #make sure the last timestep is the rest
    Pinterval = if(count == maxcount, Pe-Peday, Pinterval);
 
    ### infiltration ###
    store = max(0.0, theta_s-theta)*SD1;                      # effective storage in mm
    #Infilcap = infil_cal*hoursrain/maxcount*Ksat*mask;        # infil is a fraction of Ksat 
    
    Ip = infil_cal*hoursrain/maxcount*Ksat*((theta_s-theta)*psi/(Infilday+0.1)+1);
   # Ip = infil_cal*dt/3600*Ksat*((theta_s-theta)*psi/(Infilday+0.1)+1);
    # Green and Ampt
    Infilcap = min(store, Ip);      
    # infiltration in mm, is smallest of storage or potential infil
    Infil = min(Infilcap, Pinterval);
    # infiltration in mm, is smallest of Infil or rainfall
       
    Infilday = Infilday + Infil;
	# cumulative infil for this day
  
    infilcum = infilcum + Infil;
    # infil cumulative in mm on points for all days

    ### Runoff water height ###
    WH = cover(max(0, Pinterval-Infilcap)*0.001,0)*mask;  #waterheight in m avg per hour, 0.001 from mm to m
    WH *= dx/W;  # assume the flow is concentrated over 1/3 of the gridcell

    ### kinematic wave ###
    # manning's velocity and discharge and alpha: A = alphaQ^0.6    
    V = WH**(2/3)*sqrt(Grad)/N;
    Q = V * WH * W;
    alpha =(N/sqrt(Grad) * (W**(2.0/3.0)))**0.6;
    ts = ordinal(10);         # not sure what this is    
    Qn = kinematic(LDD, Q, 0, alpha, 0.6, ts, dt, dx);

    # new Q, alpha and WH after kin wave
    Qn = cover(Qn,0)*mask;
    A = alpha*(Qn**0.6);
    WH = A/W;
    # new water height   
    V = WH**(2/3)*sqrt(Grad)/N;
    # new velocity for flow erosion
	
    Vol = WH*W*dx;
    # volume water needed for sed concentration
    Volavg = Volavg + Vol;

    WHavg = WHavg + WH;
    # average wh needed for output
    #Qavg = Qavg + Qn;
    
   ###########################
   ######### Erosion #########
   ###########################
  
    ### Kinetic energy of rainfall in J/m2 and SPLASH Ds kg/cell ###

    # kinetic energy calculations for splash detachment
    # DT = direct rainfall on open part of the gridcell, 
    # LD is leaf drainage from covered part

    Intensity = Pinterval*3600/dt;
    # avg rainfall intensity in mm/h
    KE_DT = (1-Cover)*28.3*(1-0.52*exp(-0.042*Intensity))*Pinterval;          # kinetic energy of direct rainfall     
    Litter = 0.5*Cover;
    KE_LD = (1-Litter)*Cover*Pinterval*max(0, 15.3*sqrt(PH) - 5.87);            # kinetic energy of LD in J/m2 proposed by Brandt (1990)    
    KE = if(rain eq 0, 0, KE_DT + KE_LD);                     # total kinetic energy of rainfall

    ### splash detachment ###
    Ds = Splash_cal*Kfactor*KE*0.001*cellarea(); # detachment by raindrop g/J * J/m2 * 0.001 = kg/m2 * cell size = kg
    
    # correction factors:    
    Ds = Ds * (W+0.1*(dx-W))/dx;                   	# splash from the dry part (1-W) in a gridcell to the runoff flowing part W
    Ds = if(Kfactor eq -1 or Cohesion eq -1, 0,Ds); 	# no splash on hard surfaces, kfactor -1
    Ds = Ds * exp(-1.5*WH*1000);                    	# exponential decline of splash with water depth, Torri et al
    Ds = Ds * if (chanwidth gt 0, chanwidth/dx, 1.0);			     	# no splash in channel part of a cell

   ##############################################
   ### transport, flow detachment, deposition ###
   ##############################################
  
    ### transport capacity in kg/m3

    Sed = Sed + Ds;                                 	# add splash sediment to flow and cal concentration for flow detachment  
    Conc = min(840, if (Vol gt 1e-6, Sed/Vol, 0));  	# 840 kg/m3 is the highest conc before the flow is considered mudflow and mannings eq is no longer valid 

    rho_s = 2650;                 # specific density grains (quartz)  kg/m3
    omega = Grad*max(0,V-0.004);  # unit stream power  m/s
   
    TC = rho_s*Cg*(omega**Dg);  # transport capacity Govers et al. 1995
   
    Y = cover(if (Cohesion le 0, 0, 1/(2*Cohesion)),0)*mask; 
    # soil strength factor 0-1, from MMF
   
    # Y = cover(if (Cohesion le 0, 0, 0.79*exp(-0.85*Cohesion)),0)*mask; 
    # soil strength factor 0-1, from Eurosem
   
    Df  = max(TC-Conc,0)*Y*Vs*dt*W*dx;  # flow detachment in kg/celll:  kg/sec/m3 * m/s *m*m* sec = kg/cell  
    #Df = if(chanmask > 1, 0, Df);
    Df = Df * if (chanwidth gt 0, chanwidth/dx, 1.0);			     	# no splash in channel part of a cell

    # potential deposition in kg/cell (negative)   #kg/m3 * (-) * m3/cell  = kg/cell
    Dep = min(TC-Conc,0)*Vs*dt*W*dx;
    Dep = if (WH gt 0.0001, min(TC-Conc,0)*(1-exp(-dt*Vs/WH))*Vol, 0);  
    Dep = min(0, max(Dep, -Sed))*mask;       
    # not more deposition than there is sediment
    
    #exclude channel cells, these are not channel equations
    Df = if(chanmask eq 1,0,Df);   
    Dep = if(chanmask eq 1,0,Dep);
  
    # cumulative for display
    Dfcum = Dfcum + Df;    
    Depcum = Depcum + Dep;
    Dscum = Dscum + Ds;

    erosion = Df+Ds+Dep + erosion;         # cumulating soil loss  (detachment - deposition, is net loss)                       
   
    count = count + 1;   
  } until count > maxcount; 

#####################################
### THE REST OF THE WATER BALANCE ###
##################################### 
 
  ### volume obverland flow ###

  Volavg = Volavg / maxcount; # avg daily volume overland flow in m3
  Volcum = Volcum + Volavg;  # total overland flow in the area in m3 cumulative

  ### Actual Evapotranspiration ###

  # actual evapotranspiration ETa linear with soil moisture content (mm)
  ETpoint = theta_wp + (theta_fc - theta_wp)*2/3;
  ETfactor = if (theta gt ETpoint, 1.0, 0.0);
  ETfactor = if (theta lt ETpoint and theta ge theta_wp,(theta-theta_wp)/(ETpoint-theta_wp), ETfactor);
  ETfactor = if (theta lt theta_wp, 0.0, ETfactor);
  #ETfactor = cover(ETfactor, 0.0001); #????
         
  Ta = ETp * ETfactor * Cover;                                # actual transpiration (mm)
  Ea = ETp * theta/theta_s * (1-Cover);                       # actual soil evaporation (mm)
  ETa = Ea + Ta;                                              # ETa sum of the Evap and Transp  
  #ETa = if(landuse eq RICE or chanmask eq 1, ETp, ETa);                        # ETa equals ETp in case of water body
  ETa = min(ETa, SoilMoisture);                               # cannot be more than soil moisture present
 
  # graphs with average and cumulative average ETa of all cells
  ETacum = ETacum + ETa;
  ETFactor = ETa/ETp;  
  #ETfcum = ETfCum + ETFactor;
  
  # recalculating true ETFactor based on final ETa/ETp 
  ETE0fac = areaaverage(ETfactor, nLU);

  ### Percolation ###
  
  Perc = 24.0*Ksat*(theta/theta_s)**(2+3/lambda);  
  Perc = if(theta lt theta_wp, 0, Perc);
  Perc = min(SoilMoisture, Perc);
  perccum = perccum + Perc;

  ### water balance: new soil moisture ###

  SoilMoisture = SoilMoisture + (Infilday - ETa - Perc);
  SoilMoisture = min(SoilMoisture, theta_s*SD1);
  SoilMoisture = max(0, SoilMoisture);                # soil moisture cannot be negative
  theta = SoilMoisture/SD1*mask;                      # findng soil moisture at surface
  se = theta/theta_s;

#################################
### reporting maps and graphs ###
#################################
 
  report Cover_tss = timeoutput(nLU, areaaverage(Cover, nLU));    
  # report avg cover per land use type in a graph
  report etpcum_tss = maptotal(ETpcum)/nrCells;
  report eta_tss = maptotal(ETa)/nrCells;                    
  report etacum_tss = maptotal(ETacum)/nrCells;
  report ETfact_tss = maptotal(ETfactor)/nrCells;           # computing time series ETFactor

  report intccum_tss = maptotal(intccum)/nrCells;    
  report infil_tss = maptotal(infilcum)/nrCells; # cumulative infil 
  report LUtheta_tss = timeoutput(nLU,theta);

  report ptot_map = Pcum;
  report infiltot_map = infilcum;
  report intctot_map = intccum;
  report etatot_map = ETacum;
  report rocumm3_map = Volcum;  

  # cumulative water availalke for runoff in mm, can be compared to p and inf and intc
  rocum = max(0.0, Pcum-infilcum-intccum);
  report rocum_map = rocum;
  report rocum_tss = maptotal(rocum)/nrCells;

  report rofract_tss = timeoutput(outlet, maptotal(rocum)/maptotal(Pcum+1)); 
  report perc_tss = maptotal(perccum)/nrCells;

  # converting from kg/gridcell to ton/ha kg: kg to ton factor 1000 
  # gridcell area to ha: # kg/gridcell /cellarea() *10000 = kg/ha /1000 = ton/ha
  # so (10000/1000)/cellarea()
  factor = 10/cellarea();
  report ds_map = Dscum*factor;
  report df_map = Dfcum*factor;
  report dep_map = Depcum*factor;

  # soil loss is the net positive detachment in ton/ha, expressed per gridcell. Note that a gridcell is less than a ha 
  soilloss = max(0, erosion*factor);            
        
  report Soilloss_map = soilloss;
  report Soilloss_tss= maptotal(soilloss)/nrCells;   # area avg soilloss ton/ha
  report Soillosslu_tss = timeoutput(nLU, soilloss); # soilloss per land unit ton/ha

  # classify with FAO classification
  erosrate = scalar(
    if(soilloss ge 0 and soilloss lt 2, 0, 
    if(soilloss ge 2 and soilloss lt 5, 1, 
    if(soilloss ge 5 and soilloss lt 10, 2, 
    if(soilloss ge 10 and soilloss lt 20, 3, 
    if(soilloss ge 20 and soilloss lt 50, 4, 
    if(soilloss ge 50 and soilloss lt 100, 5, 6)))))));
  report erosrate_map = ordinal(erosrate);

  # set everything to zero for the next day, 
  # we assume there is no runoff that lasts until the next day
  Sed = 0;
  Conc = 0;
  Vol = 0;
  WH = 0;  

  # live to see another day
  day = day + 1;
